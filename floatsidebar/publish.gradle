apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.0'
group 'com.aceegg.sidebar'

def siteUrl = 'https://github.com/imcloud/FloatSideBar'    // project homepage
def gitUrl = 'https://github.com/imcloud/FloatSideBar.git' // project git

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/floatsidebar-release.aar")
            groupId this.group
            artifactId 'FloatSideBar'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'SideBar'
        name = 'FloatSideBar'
        description = "仿索尼Z1手机通讯录侧边栏"
        publish = true
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        dryRun = true
        version {
            name = this.version
            desc = "${this.version}-release"
            released = new Date()
            vcsTag = this.version
        }
    }
}